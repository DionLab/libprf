IF(PRF_CORE_DISPATCH)
	################################ FASTA ##################################
	IF(PRF_INPUT_FASTA)
		IF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/FASTA)
			FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/FASTA)
		ENDIF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/FASTA)
		pumpit(${CMAKE_CURRENT_SOURCE_DIR}/FASTA/threads.c.pump
		       ${CMAKE_CURRENT_BINARY_DIR}/FASTA/threads.c)
		pumpit(${CMAKE_CURRENT_SOURCE_DIR}/FASTA/threadpool.c.pump
		       ${CMAKE_CURRENT_BINARY_DIR}/FASTA/threadpool.c)
		SET(FASTA_DISPATCH_SOURCES 
				FASTA/dispatch.c
				${CMAKE_CURRENT_BINARY_DIR}/FASTA/threads.c
				${CMAKE_CURRENT_BINARY_DIR}/FASTA/threadpool.c
				)
		SET_PROPERTY(SOURCE ${CMAKE_CURRENT_BINARY_DIR}/FASTA/threads.c
												${CMAKE_CURRENT_BINARY_DIR}/FASTA/threadpool.c
								 APPEND PROPERTY COMPILE_FLAGS
								         "-I${CMAKE_CURRENT_SOURCE_DIR}/FASTA"
		)
	ENDIF(PRF_INPUT_FASTA)
	
	################################ PacBio #################################
	IF(PRF_INPUT_HDF5 OR PRF_INPUT_PBBAM)
		IF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/PacBio)
			FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/PacBio)
		ENDIF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/PacBio)
 		pumpit(${CMAKE_CURRENT_SOURCE_DIR}/PacBio/threadpools.c.pump
 		       ${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpools.c)
 		pumpit(${CMAKE_CURRENT_SOURCE_DIR}/PacBio/threadpoolz.c.pump
 		       ${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpoolz.c)
		SET_PROPERTY(SOURCE ${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpools.c
												${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpoolz.c
								 APPEND PROPERTY COMPILE_FLAGS
								         "-I${CMAKE_CURRENT_SOURCE_DIR}/PacBio"
		)
		SET(PB_DISPATCH_SOURCES 
				${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpools.c
				${CMAKE_CURRENT_BINARY_DIR}/PacBio/threadpoolz.c)
		IF (PRF_INPUT_HDF5)
			SET(PB_DISPATCH_SOURCES ${PB_DISPATCH_SOURCES};PacBio/dispatchHDF5.c)
		ENDIF(PRF_INPUT_HDF5)
		IF(PRF_INPUT_PBBAM)
			SET(PB_DISPATCH_SOURCES ${PB_DISPATCH_SOURCES};PacBio/dispatchBAM.cpp)
			SET_PROPERTY(SOURCE PacBio/dispatchBAM.cpp APPEND PROPERTY COMPILE_FLAGS "-std=gnu++11 -fpermissive")
		ENDIF(PRF_INPUT_PBBAM)
	ENDIF(PRF_INPUT_HDF5 OR PRF_INPUT_PBBAM)

	################################ Random #################################
	#SET(RD_DISPATCH_SOURCES dispatch/Random/dispatch.c dispatch/Random/rd_threads.c dispatch/Random/RandomData.c)

	#########################################################################
	SET(DISPATCH_SOURCES ${PB_DISPATCH_SOURCES} ${FASTA_DISPATCH_SOURCES} ${RD_DISPATCH_SOURCES})

	IF(DISPATCH_SOURCES)
		SET(DISPATCH_SOURCES "${DISPATCH_SOURCES};threadpool.c;compare.c;GetDispatchThreadIndex.c")
		IF(BUILD_STATIC_LIBS)
			ADD_LIBRARY(DISPATCH_STATIC OBJECT ${DISPATCH_SOURCES})
		ENDIF(BUILD_STATIC_LIBS)
		IF(BUILD_SHARED_LIBS)
			ADD_LIBRARY(DISPATCH_SHARED OBJECT ${DISPATCH_SOURCES})
			SET_PROPERTY(TARGET DISPATCH_SHARED PROPERTY POSITION_INDEPENDENT_CODE ON)
		ENDIF(BUILD_SHARED_LIBS)

		IF(PRF_INPUT_HDF5)
				IF(BUILD_STATIC_LIBS)
					SET_PROPERTY(TARGET DISPATCH_STATIC APPEND PROPERTY INCLUDE_DIRECTORIES "${HDF5_C_INCLUDE_DIR}")
					IF(BUILD_HDF5)
						ADD_DEPENDENCIES(DISPATCH_STATIC libHDF5)
					ENDIF(BUILD_HDF5)
				ENDIF(BUILD_STATIC_LIBS)
				IF(BUILD_SHARED_LIBS)
					SET_PROPERTY(TARGET DISPATCH_SHARED APPEND PROPERTY INCLUDE_DIRECTORIES "${HDF5_C_INCLUDE_DIR}")
					IF(BUILD_HDF5)
						ADD_DEPENDENCIES(DISPATCH_SHARED libHDF5)
					ENDIF(BUILD_HDF5) 
				ENDIF(BUILD_SHARED_LIBS)
		ENDIF(PRF_INPUT_HDF5)
		IF(PRF_INPUT_PBBAM)
			IF(BUILD_STATIC_LIBS)
				ADD_DEPENDENCIES(DISPATCH_STATIC PacBioBAM)
				SET_PROPERTY(TARGET DISPATCH_STATIC APPEND PROPERTY INCLUDE_DIRECTORIES "${PBBAM_INCLUDE_DIRS};${HTSLIB_DIR}")
				SET_PROPERTY(TARGET DISPATCH_STATIC APPEND PROPERTY LINK_DEPENDS ${HTSLIB_LIBRARIES})
			ENDIF(BUILD_STATIC_LIBS)
			IF(BUILD_SHARED_LIBS)
				ADD_DEPENDENCIES(DISPATCH_SHARED PacBioBAM)
				SET_PROPERTY(TARGET DISPATCH_SHARED APPEND PROPERTY INCLUDE_DIRECTORIES "${PBBAM_INCLUDE_DIRS};${HTSLIB_DIR}")
				SET_PROPERTY(TARGET DISPATCH_SHARED APPEND PROPERTY LINK_DEPENDS ${HTSLIB_LIBRARIES})
			ENDIF(BUILD_SHARED_LIBS)
		ENDIF(PRF_INPUT_PBBAM)
		
		SET(HAS_DISPATCH TRUE PARENT_SCOPE)
	ENDIF(DISPATCH_SOURCES)
ENDIF(PRF_CORE_DISPATCH)
